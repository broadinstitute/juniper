plugins {
    id 'bio.terra.java-spring-conventions'
    id 'de.undercouch.download'
    id 'com.google.cloud.tools.jib'
    id 'com.srcclr.gradle'
    id 'org.sonarqube'

    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
}

apply from: 'generators.gradle'
apply from: 'publishing.gradle'


dependencies {

    implementation 'bio.terra:terra-common-lib'
    implementation project(':core')
    implementation project(':populate')
    implementation project(':ui-admin')
    implementation 'org.apache.commons:commons-dbcp2'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'com.sendgrid:sendgrid-java:4.0.1'

    implementation 'com.auth0:java-jwt:4.2.2'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5',
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            //'org.bouncycastle:bcprov-jdk15on:1.70',
            'io.jsonwebtoken:jjwt-jackson:0.11.5' // or 'io.jsonwebtoken:jjwt-gson:0.11.5' for gson

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        // Fixes warning about multiple occurrences of JSONObject on the classpath
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testImplementation 'org.mockito:mockito-inline'
}

task copyWebApp(type: Copy) {
    from "$rootDir/ui-admin/build"
    into "$rootDir/api-admin/build/resources/main/static"
}

// for now, only jib depends on copyWebApp, so that a npm rebuild/install will not be triggered for
// development redeploys.  this means to deploy locally with the static assets in place you'll
// need to run the copyWebApp task yourself
copyWebApp.dependsOn(':ui-admin:bundle')
jibDockerBuild.dependsOn('copyWebApp')


test {
    useJUnitPlatform ()
}

sonarqube {
    properties {
        property 'sonar.projectName', 'terra-java-project-template'
        property 'sonar.projectKey', 'terra-java-project-template'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}
