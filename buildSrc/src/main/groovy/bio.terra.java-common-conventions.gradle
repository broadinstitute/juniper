
plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless'
    id 'com.github.spotbugs'
    id 'org.hidetake.swagger.generator'
}

boolean isCiServer = System.getenv().containsKey("CI")

if (!isCiServer) {
    tasks.withType(JavaExec).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.2'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.31'

    implementation 'org.slf4j:slf4j-api'

    testImplementation 'org.hamcrest:hamcrest:2.2'

    implementation 'bio.terra:terra-common-lib:0.1.10-SNAPSHOT'
    //terra-common-lib pulls in google-oauth-client v1.33.2, which contains vuln CVE-2021-22573
    //This pulls in a patched version of google-oauth-client that supersedes the vulnerable version
    //This is a workaround until TCL adopts a patched version
    implementation 'com.google.oauth-client:google-oauth-client:1.33.3'
    implementation 'bio.terra:datarepo-jakarta-client:1.565.0-SNAPSHOT'

    implementation 'io.opentelemetry:opentelemetry-api:1.31.0'
    implementation 'org.springframework:spring-aspects:6.0.11'
}

tasks.named('test') {
    useJUnitPlatform()
}

version = gradle.releaseVersion
group = 'bio.terra'

spotless {
    java {
        targetExclude "${buildDir}/**"
        targetExclude "**/swagger-code/**"
        googleJavaFormat()
    }
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isCiServer) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}

// Spotbugs configuration
spotbugs {
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf("HIGH")
    effort = com.github.spotbugs.snom.Effort.MAX
}
spotbugsMain {
    reports {
        if (isCiServer) {
            sarif.enabled = true
        } else {
            html.enabled = true
        }
    }
}

jacocoTestReport {
    reports {
        // sonarqube requires XML coverage output to upload coverage data
        xml.required = true
    }
}

// See https://github.com/spotbugs/spotbugs-gradle-plugin/issues/1197
ext['commons-lang3.version']='3.13.0'
